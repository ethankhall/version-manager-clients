apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

apply from: "$rootDir/gradle/publishing.gradle"

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile gradleApi()
    compile project(path: ':java-client', configuration: 'shadow')

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'com.squareup.okhttp3:mockwebserver:3.2.0'
    testCompile 'com.netflix.nebula:nebula-test:3.1.0'
}

gradlePlugin {
    plugins {
        cromPlugin {
            id = "tech.crom.version-manager"
            implementationClass = "tech.crom.client.gradle.VersionManagerPlugin"
        }
    }
}

bintray.publications = ((bintray.publications as List<String>) + ['pluginMaven', 'cromPluginPluginMarkerMaven']) as String[]

publishing.publications.all {
    pom.withXml {
        def dep = asNode().dependencies.dependency.find {
            it.groupId.text() == 'tech.crom.client' && it.artifactId.text() == 'java-client'
        }

        if (dep) {
            dep.appendNode('classifier', 'all')
        }
    }
}

bintrayUpload.dependsOn('publishCromPluginPluginMarkerMavenPublicationToMavenLocal')
bintrayUpload.dependsOn('publishPluginMavenPublicationToMavenLocal')