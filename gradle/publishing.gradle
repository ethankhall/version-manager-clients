apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "ethankhall"
            name "Ethan Hall"
            email "ethan@ehdev.io"
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mainJava']
    pkg {
        repo = 'crom'
        name = project.name
        userOrg = 'ethankhall'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ethankhall/version-manager-clients.git'
        version {
            name = project.version
            released = new Date()
            vcsTag = "v${ project.version }"
        }
    }
}

publishing {
    publications {
        mainJava(MavenPublication) {
            from components.java

            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.description)
                root.appendNode('name', project.name)
                root.appendNode('url', "https://github.com/ethankhall/version-manager-clients")
                root.children().last() + pomConfig
            }
        }
    }
}